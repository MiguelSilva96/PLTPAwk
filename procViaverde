#!/usr/bin/gawk -f

BEGIN {
    FS = "[<>]";
    file = "index.html";
    fmtEntradas = "<li><b>Dia %s:</b> %d (%.2f€)</li>\n";
    fmtTotais = "<li>Total gasto apenas em %s: %.2f€</li>\n";
    fmtli = "<li>%s</li>\n";
    fmth5 = "<h5 class='bg-3'>%s</h5>\n";
    fmth4 = "<h4 class='bg-2'>%s</h4>\n";
    fmth2 = "<h2>%s</h2></div>\n";
    ini = "<!DOCTYPE html> <html> <head>\n";
    tit = "<title> Via Verde </title>";
    enc = "<meta charset='utf-8'/> </head> <body>";
    end = "</body> </html>";
    beg = "<div class='bg-4 title'><h1> Via Verde </h1>";
    css = "<link rel='stylesheet' href='css/styles.css'>";
    ref = "<a target='_blank' href='%s'>%s</a>";
    maps = "https://www.google.pt/maps/place/";
    infoCliente = 0;
}

NR == 1 {
    print ini > file;
    print tit > file;
    print css > file;
    print enc > file;
    print beg > file;
}

$2~/MES_EMISSAO/{
    s = "Mês Emissão: " $3;
    printf(fmth2, s) > file;
}

$2~/NIF/{
    infoCliente++;
    print "<ul>" > file;
}

infoCliente {
    str = "<b>"$2"</b>: "$3;
    gsub("_"," ", str);
    printf(fmtli, str) > file;

    if($2~/CODIGO_POSTAL/) {
        print "</ul>" > file;
        infoCliente--;
    }
}

$2~/DATA_ENTRADA/ && $3~/[0-9-]+/ {
    dias[$3]++;
    ultDia = $3;
}

$4~/\/SAIDA/ {
    locaisSaida[$3]++;
}

$4~/\/ENTRADA/ {
    locaisEntra[$3]++;
}

$2~/IMPORTANCIA/ {
    gsub(",",".", $3);
    totalGasto += $3;
    ultimoGasto = $3;
    totalDias[ultDia] += $3;
}

$2~/VALOR_DESCONTO/ {
    gsub(",", ".", $3);
    totalGasto  -= $3;
    ultimoGasto -= $3;
    totalDias[ultDia] -= $3;
}

$2~/TIPO/ {
    totalLocais[$3] += ultimoGasto;
}

END {
    str = "<h2 class='bg-4'>Informação:</h2>";
    print str > file;

    str = "Nº de 'entradas' em cada dia do mês:";
    printf(fmth4, str) > file;
    print "<ul>" > file;
    for (i in dias) 
        printf(fmtEntradas, i, dias[i], totalDias[i]) > file;
    print "</ul>" > file;


    printf(fmth4, "Locais de ’saída’:") > file;
    print "<ul>" > file;
    for (i in locaisSaida) {
        aux = sprintf(ref, maps i, i);
        str = aux " " "("locaisSaida[i]")";
        printf(fmtli, str) > file;
    }
    print "</ul>" > file;


    str = "Total gasto no mês:";
    printf(fmth4, str) > file;
    str = "Total:" " " totalGasto"€";
    printf(fmth5, str) > file;
    print "<ul>" > file;
    for(i in totalLocais)
        printf(fmtTotais, i, totalLocais[i]) > file;
    print "</ul>" > file;

    print end > file;
}
