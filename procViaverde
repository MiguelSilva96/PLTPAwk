#!/usr/bin/gawk -f

BEGIN {
    FS = "[<>]";
    file = "index.html";
    icon = "<i class='fa fa-calendar'>"
    fmtEntrad = "<li>"icon" <b>Dia %s:</b> %d (Gastou %.2f€)</i></li>\n";
    fmtTotais = "<li>Total gasto apenas em %s: %.2f€</i></li>\n";
    fmtli = "<li> %s</li>\n";
    fmth5 = "<h5 class='bg-3'>%s</h5>\n";
    fmth4 = "<h4 class='bg-2'>%s</h4>\n";
    fmth2 = "<h2>%s</h2></div>\n";
    icon = "<i class='fa fa-road'>";
    ini = "<!DOCTYPE html> <html> <head>\n";
    tit = "<title> Via Verde </title>";
    enc = "<meta charset='utf-8'/> </head> <body>";
    end = "</body> </html>";
    beg = "<div class='bg-4 title'><h1>"icon" Via Verde </i></h1>";
    css = "<link rel='stylesheet' href='css/styles.css'>";
    fta = "<link rel='stylesheet' href='css/fa/css/font-awesome.css'>";
    ref = "<a target='_blank' href='%s'>%s</a>";
    maps = "https://www.google.pt/maps/place/";
    infoCliente = 0;
}

NR == 1 {
    print ini > file;
    print tit > file;
    print css > file;
    print fta > file;
    print enc > file;
    print beg > file;
}

$2~/MES_EMISSAO/{
    s = "Mês Emissão: " $3;
    printf(fmth2, s) > file;
}

$2~/NIF/{
    infoCliente++;
    print "<ul>" > file;
}

infoCliente {
    icon = "<i class='fa fa-user-circle'> ";
    str = icon"<b>"$2"</b>:" $3"</i>";
    gsub("_"," ", str);
    printf(fmtli, str) > file;

    if($2~/CODIGO_POSTAL/) {
        print "</ul>" > file;
        infoCliente--;
    }
}

$2~/DATA_ENTRADA/ && $3~/[0-9-]+/ {
    dias[$3]++;
    ultDia = $3;
}

$4~/\/SAIDA/ {
    locaisSaida[$3]++;
}

$2~/HORA_ENTRADA/ {
    horaEntrada = $3;
}

$2~/HORA_SAIDA/ {
    horaSaida = $3;
    i = duracao(horaEntrada, horaSaida);
    duracoes[ultDia] += i;
}

$2~/IMPORTANCIA/ {
    gsub(",",".", $3);
    totalGasto += $3;
    ultimoGasto = $3;
    totDias[ultDia] += $3;
}

$2~/VALOR_DESCONTO/ {
    gsub(",", ".", $3);
    totalGasto  -= $3;
    ultimoGasto -= $3;
    totDias[ultDia] -= $3;
}

$2~/TIPO/ {
    totalLocais[$3] += ultimoGasto;
}

END {
    str = "<h2 class='bg-4'>Informação:</h2>";
    print str > file;

    str = "Nº de entradas em cada dia do mês:";
    printf(fmth4, str) > file;
    print "<ul>" > file;
    for (i in dias) {
        printf(fmtEntrad, i, dias[i], totDias[i]) > file;
        icon = "<i class='fa fa-clock-o'> ";
        aux = "Tempo total em viagem: ";
        str = icon aux duracoes[i] " minutos</i>";
        printf(fmth5, str) > file;

    }
    print "</ul>" > file;


    printf(fmth4, "Locais de saída:") > file;
    print "<ul>" > file;
    for (i in locaisSaida) {
        aux = sprintf(ref, maps i, i);
        icon = "<i class='fa fa-map-marker'> ";
        str = icon aux " </i>" "("locaisSaida[i]")";
        printf(fmtli, str) > file;
    }
    print "</ul>" > file;


    str = "Total gasto no mês:";
    printf(fmth4, str) > file;
    str = "Total:" " " totalGasto"€";
    print "<div class='aux'>" > file;
    printf(fmth2, str) > file;
    print "<ul>" > file;
    for(i in totalLocais)
        printf(fmtTotais, i, totalLocais[i]) > file;
    print "</ul>" > file;

    print end > file;
}

#Args: he - Hora entrada
#      hs - Hora saida
#Return duracao em minutos
function duracao(he, hs) {
    result = 0;
    split(he, ae, /[:]/);
    split(hs, as, /[:]/);
    if(ae[1]=="" || as[1]=="")
        return 0;
    if(as[1]>=ae[1])
        aux = (as[1]-ae[1])*60;
    else
        aux = (as[1] + (24 - ae[1]))*60;
    result += aux;
    result -= ae[2];
    result += as[2];
    return result;
}