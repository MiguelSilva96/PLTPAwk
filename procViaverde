#!/usr/bin/gawk -f

BEGIN {
    FS = "[<>]";
    fmtEntradas = "<li>Dia %s: %d (%.2f€)</li>\n";
    fmtTotais = "<li>Total gasto apenas em %s: %.2f€</li>\n";
    fmtli = "<li>%s</li>\n";
    fmth4 = "<h4>%s</h4>\n";
    fmth2 = "<h2>%s</h2>\n";
    ini = "<html> <head>\n";
    tit = "<title> Via Verde </title>";
    enc = "<meta charset='utf-8'/> </head> <body>";
    end = "</body> </html>";
    beg = "<center><h1> Via Verde </h1></center>";
    infoCliente = 0;
}

NR == 1 {
    print ini > "index.html";
    print tit > "index.html";
    print enc > "index.html";
    print beg > "index.html";
}

$2~/MES_EMISSAO/{
    s = "Mês Emissão: " $3;
    printf(fmth2, s) > "index.html";
    s = "Cliente: "
    printf(fmth2, s) > "index.html";
}

$2~/NIF/{
    infoCliente++;
}

infoCliente {
    str = "<b>"$2"</b>: "$3;
    printf(fmtli, str) > "index.html";
    if($2~/CODIGO_POSTAL/)
        infoCliente--;
}

$2~/DATA_ENTRADA/ && $3~/[0-9-]+/ {
    dias[$3]++;
    ultDia = $3;
}

$4~/\/SAIDA/ {
    locaisSaida[$3]++;
}

$4~/\/ENTRADA/ {
    locaisEntra[$3]++;
}

$2~/IMPORTANCIA/ {
    gsub(",",".", $3);
    totalGasto += $3;
    ultimoGasto = $3;
    totalDias[ultDia] += $3;
}

$2~/VALOR_DESCONTO/ {
    gsub(",", ".", $3);
    totalGasto  -= $3;
    ultimoGasto -= $3;
    totalDias[ultDia] -= $3;
}

$2~/TIPO/ {
    totalLocais[$3] += ultimoGasto;
}

END {
    print "<h2>Informação:</h2>" > "index.html";
    str = "Nº de 'entradas' em cada dia do mês:";
    printf(fmth4, str) > "index.html";
    for (i in dias) 
        printf(fmtEntradas, i, dias[i], totalDias[i]) > "index.html";

    printf(fmth4, "Locais de ’saída’:") > "index.html";
    for (i in locaisSaida) {
        str = i " " "("locaisSaida[i]")";
        printf(fmtli, str) > "index.html";
    }

    str = "Total gasto no mês:";
    printf(fmth4, str) > "index.html";
    print "Total:", totalGasto"€" > "index.html";
    for(i in totalLocais)
        printf(fmtTotais, i, totalLocais[i]) > "index.html";

    print end > "index.html";
}